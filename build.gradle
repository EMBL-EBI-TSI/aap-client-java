
buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("pl.allegro.tech.build:axion-release-plugin:1.8.1")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'pl.allegro.tech.build.axion-release'

    group = 'uk.ac.ebi.tsc.aap.client'
    scmVersion {
        tag {
            prefix = 'aap' // this must be followed by a - in the actual tags
        }
    }
    version = scmVersion.version

    repositories {
        maven { url "https://oss.sonatype.org/content/groups/public" }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile("org.springframework.boot:spring-boot-starter")
        compile("com.fasterxml.jackson.core:jackson-annotations:2.8.11") // from 2.9, cannot use @JsonIgnore
        testCompile("com.google.guava:guava:24.1-jre")
        testCompile('org.springframework.boot:spring-boot-starter-test')
        compile (group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5')
    }

    bootRepackage {
        enabled = false
    }

    task sourceJar(type: Jar) { from sourceSets.main.allJava }

    task testJar(type: Jar, dependsOn: testClasses) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    artifacts {
        archives jar
        archives sourceJar
        archives testJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar { classifier "sources" }
                artifact testJar { classifier "tests" }
            }
        }
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'aap-client-java'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'EBI TSC AAP Client'
                    url 'https://github.com/EMBL-EBI-TSI/aap-client-java'

                    scm {
                        url 'https://github.com/EMBL-EBI-TSI/aap-client-java'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'ameliec'
                            name 'Amélie Cornélis'
                            email 'ameliec@ebi.ac.uk'
                        }
                        developer {
                            id 'neilg'
                            name 'Neil Goodgame'
                            email 'neilg@ebi.ac.uk'
                        }
                    }
                }
            }
        }
    }
}